pipeline {
	agent { label "${params.AgentLabel}" }
	
	environment {
		GIT_URL = credentials('dsp56300_gitUrl')
		BUILD_JUCE = needsJuce()
		BUILD_FX_PLUGIN = buildFxPlugins()
		CMAKE_BUILD_DIR = 'temp/cmake_jenkins'
	}
	parameters
	{
		string(name: 'Branch', defaultValue: "master", description: '')
		string(name: 'AgentLabel', defaultValue: "git && cmake", description: '')
		booleanParam(name: 'FXPlugins', defaultValue: false, description: '') 
		booleanParam(name: 'Deploy', defaultValue: true, description: '') 
		booleanParam(name: 'IntegrationTests', defaultValue: true, description: '')
		booleanParam(name: 'Upload', defaultValue: false, description: '') 
	}
	stages {
		stage("Checkout") {
			steps {
				script {
					currentBuild.displayName += " - ${params.AgentLabel} - ${params.Branch}"
					currentBuild.description = "Integration Tests: ${params.IntegrationTests}\nDeploy: ${params.Deploy}\nUpload: ${params.Upload}"
				}
				doCheckout()
			}
		}
		stage('Compile') {
			steps {
				cmakeBuild();
			}
		}
		stage('Integration Tests') {
			when {
				expression {
					return fileExists(getIntegrationTestRunner()) && params.IntegrationTests && !"${env.NODE_LABELS}".contains('atom')
				}
			}
			steps {
				doIntegrationTests();
			}
		}
		stage('Pack') {
			when {
				expression {
					return params.Deploy || params.Upload
				}
			}
			steps {
				doPack();
			}
		}
		stage('Deploy') {
			when {
				expression {
					return params.Deploy
				}
			}
			steps {
				doDeploy();
			}
		}
		stage('Upload') {
			when {
				expression {
					return params.Upload
				}
			}
			steps {
				doUpload();
			}
		}
	}
}

def genericSh(cmd)
{
	if (isUnix())
	{
		sh cmd
	}
	else
	{
		bat cmd
	}
}

def needsJuce()
{
	return params.Deploy ? 'ON' : 'OFF'
}
def buildFxPlugins()
{
	return params.FXPlugins ? 'ON' : 'OFF'
}
def cmakeBuildDir()
{
	return "${CMAKE_BUILD_DIR}_${params.Branch}"
}

def copyDataFrom(from, to)
{
	withCredentials([file(credentialsId: 'rclone_dsp56300_conf', variable: 'rcloneConfig')]) {
		genericSh 'rclone --config ' + formatArg('rcloneConfig') + " sync dsp56300:${from} ${to}"
	}
}

def doCheckout()
{
	checkout(
		[$class: 'GitSCM'
		, branches: [[name: "*/${params.Branch}"]]
		, extensions: [
			[$class: 'SubmoduleOption'
			, disableSubmodules: false
			, parentCredentials: false
			, recursiveSubmodules: true
			, reference: ''
			, trackingSubmodules: false]]
			, userRemoteConfigs: [[ url: GIT_URL ]]
		]
	)
}

def cmakeBuild()
{
	if(needsJuce()) {
		copyVst2Sdk()
	}
	dir(cmakeBuildDir()) {
		deleteFile('CMakeCache.txt')	// we need to do this to be able to change option in the command line below. Otherwise, they won't change for an existing project
	}
	def buildDir = cmakeBuildDir();
	genericSh "cmake . -B ${buildDir} -Dgearmulator_BUILD_JUCEPLUGIN=${BUILD_JUCE} -Dgearmulator_BUILD_FX_PLUGIN=${BUILD_FX_PLUGIN} -DCMAKE_BUILD_TYPE=Release"
	dir("${buildDir}") {
		genericSh 'cmake --build . --config Release'
	}
}

def copyTestData()
{
	copyDataFrom('integrationtests', './testData')
}

def copyVst2Sdk()
{
	copyDataFrom('vstsdk2.4.2/', 'source/vstsdk2.4.2/')
}

def getIntegrationTestRunner()
{
	def buildDir = cmakeBuildDir()
	if(isUnix()) {
		return "${env.WORKSPACE}/${buildDir}/source/virusIntegrationTest/virusIntegrationTest"
	} else {
		def foo = "${env.WORKSPACE}\\${buildDir}\\source\\virusIntegrationTest\\Release\\virusIntegrationTest.exe"
		echo "${foo}"
		return "${foo}"
	}
}

def doIntegrationTests()
{
	dir(cmakeBuildDir()) {
		def runner = getIntegrationTestRunner()
		
		copyTestData()
		
		dir('testData') {
			def files = findFiles() 
	 
			files.each{ f -> 
				if(f.directory) {
					dir(f.name) {
						def file = readFile 'presets.txt'
						file.readLines().each {
							genericSh "${runner} -rom \"${f.name}\" -preset \"${it}\""
						}
					}
				}
			}
		}
	}
}

def formatArg(_arg)
{
	echo 'Formatting arg ' + _arg

	if(isUnix())
		return '$' + _arg
	else
		return '%' + _arg + '%'
}
def copyArtefacts(rcloneConfig, targetFolder)
{
	dir(cmakeBuildDir()) {
		genericSh 'rclone copy --config ' + formatArg('rcloneConfig') + ' --transfers=1 -v --include "*.zip" --include "*.deb" --include "*.rpm" --min-size 100 --max-depth 1 . ' + "${targetFolder}/${params.Branch}"
	}
}

def deleteFile(name)
{
	if(isUnix())
		sh "rm -f ${name}"
	else
		bat "del ${name}"
}

def doPack()
{
	dir(cmakeBuildDir()) {
		deleteFile('*.zip')
		deleteFile('*.rpm')
		deleteFile('*.deb')
		genericSh 'cpack -G ZIP'
		
		if(isUnix())
		{
			sh '''
			if [ "$(uname)" != "Darwin" ]; then
				cpack -G DEB
				cpack -G RPM
			fi
			'''
		}
	}
}

def doDeploy()
{
	withCredentials([file(credentialsId: 'rclone_dsp56300_conf', variable: 'rcloneConfig')]) {
		copyArtefacts("${rcloneConfig}", "dsp56300:deploy");
	}
}

def doUpload()
{
	withCredentials([file(credentialsId: 'rclone_dsp56300_conf', variable: 'rcloneConfig')]) {
		copyArtefacts("${rcloneConfig}", "dsp56300_ftp:builds")
	}
}
