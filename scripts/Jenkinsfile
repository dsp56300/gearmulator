pipeline {
	agent { label "${params.AgentLabel}" }
	
	environment {
		GIT_URL = credentials('dsp56300_gitUrl')
		CMAKE_BUILD_DIR = 'temp/cmake_jenkins'
	}
	parameters
	{
		string(name: 'Branch', defaultValue: "master", description: '')
		string(name: 'AgentLabel', defaultValue: "git && cmake", description: '')
		string(name: 'Synths', defaultValue: "-Dgearmulator_SYNTH_OSIRUS=on", description: '')
		string(name: 'DisplayName', defaultValue: "?", description: '')
		string(name: 'UploadFolder', defaultValue: "", description: '')
		booleanParam(name: 'FXPlugins', defaultValue: false, description: '') 
		booleanParam(name: 'Deploy', defaultValue: true, description: '') 
		booleanParam(name: 'IntegrationTests', defaultValue: true, description: '')
		booleanParam(name: 'Upload', defaultValue: false, description: '') 
	}
	stages {
		stage("Checkout") {
			steps {
				script {
					currentBuild.displayName += " - ${params.AgentLabel} - ${params.Branch} - ${params.DisplayName}"
					currentBuild.description = "Integration Tests: ${params.IntegrationTests}\nDeploy: ${params.Deploy}\nUpload: ${params.Upload}"
				}
				doCheckout()
			}
		}
		stage('Compile') {
			steps {
				cmakeBuild();
			}
		}
		stage('Pack') {
			steps {
				doPack();
			}
		}
		stage('Integration Tests') {
			when {
				expression {
					return params.IntegrationTests
				}
			}
			steps {
				doIntegrationTests();
			}
		}
		stage('Deploy') {
			when {
				expression {
					return params.Deploy
				}
			}
			steps {
				doDeploy();
			}
		}
		stage('Upload') {
			when {
				expression {
					return params.Upload
				}
			}
			steps {
				doUpload();
			}
		}
	}
}

def genericSh(cmd)
{
	if (isUnix())
	{
		sh cmd
	}
	else
	{
		bat cmd
	}
}

def formatArg(_arg)
{
	if(isUnix())
		return '$' + _arg
	else
		return '%' + _arg + '%'
}

def deleteFile(name)
{
	if(isUnix())
		sh "rm -f ${name}"
	else
		bat "del ${name}"
}

def buildFxPlugins()
{
	return params.FXPlugins ? 'on' : 'off'
}

def cmakeBuildDir()
{
	return "${CMAKE_BUILD_DIR}_${params.Branch}"
}

def doCheckout()
{
	checkout(
		[$class: 'GitSCM'
		, branches: [[name: "*/${params.Branch}"]]
		, extensions: [
			[$class: 'SubmoduleOption'
			, disableSubmodules: false
			, parentCredentials: false
			, recursiveSubmodules: true
			, reference: ''
			, trackingSubmodules: false]]
			, userRemoteConfigs: [[ url: GIT_URL ]]
		]
	)
}

def getSynths()
{
    return "${env.synths}";
}

def cmakeBuild()
{
    def buildDir = cmakeBuildDir()
    def synths = getSynths()
    def fx = buildFxPlugins()

	withCredentials([file(credentialsId: 'rclone_dsp56300_conf', variable: 'RCLONE_CONF')]) {
    	genericSh "cmake -Dgearmulator_SOURCE_DIR=../ -Dgearmulator_BINARY_DIR=../${buildDir} -Dgearmulator_BUILD_JUCEPLUGIN=on -Dgearmulator_BUILD_FX_PLUGIN=${fx} -DCMAKE_BUILD_TYPE=Release ${synths} -P scripts/generate.cmake"
	}

	genericSh "cmake --build ${buildDir} --config Release"
}

def doIntegrationTests()
{
	withCredentials([file(credentialsId: 'rclone_dsp56300_conf', variable: 'RCLONE_CONF')]) {
    	dir(cmakeBuildDir()) {
    		genericSh "ctest -C Release -VV --output-on-failure"
    	}
	}
}

def doPack()
{
    def buildDir = cmakeBuildDir()
    
	dir(cmakeBuildDir()) {
		genericSh 'cmake -P ../../scripts/pack.cmake'
	}
	if("${params.UploadFolder}" == "donators") {
		genericSh "cmake -Dgearmulator_BINARY_DIR=${WORKSPACE}/${buildDir} -P scripts/packagesource.cmake"
	}
}

def copyArtefacts(local, remote)
{
    def buildDir = cmakeBuildDir()
    def synths = getSynths()
    
	withCredentials([file(credentialsId: 'rclone_dsp56300_conf', variable: 'RCLONE_CONF')]) {
		genericSh "cmake -Dgearmulator_BINARY_DIR=${buildDir} -DUPLOAD_LOCAL=${local} -DUPLOAD_REMOTE=${remote} -DFOLDER=${params.UploadFolder} ${synths} -P scripts/deployAll.cmake"
	}
}

def doDeploy()
{
	copyArtefacts(1,0)
}

def doUpload()
{
	copyArtefacts(0,1)
}
